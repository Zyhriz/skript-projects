# requires skript-reflect
# CODE BY ZYHRIZ. DO NOT REMOVE THIS INFO
# Allows you to check your server's memory stats (free/max/used) **without SkUtilities**.
# Make sure this file is loaded before any other file that relies upon it.

import:
	java.lang.Runtime

expression [the ][server['s] ](available|free) (memory|ram):
	get:
		set {_n} to Runtime.getRuntime().freeMemory()/1000000
		return {_n}

expression [the ][server['s] ]max[imum] (memory|ram):
	get:
		set {_n} to Runtime.getRuntime().maxMemory()/1000000
		return {_n}

expression [the ][server['s] ]used (memory|ram):
	get:
		set {_n} to max ram - free ram
		return {_n}

expression percent[age][ of [the ][server['s] ](memory|ram)] used[ up]:
	get:
		set {_p} to used memory/max memory
		return {_p}

expression percent[age][ of [the ][server['s] ](memory|ram)] free[d]:
	get:
		set {_p} to free memory/max memory
		return {_p}
