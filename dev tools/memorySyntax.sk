# requires skript-reflect
# CODE BY ZYHRIZ. DO NOT REMOVE THIS INFO
# This file creates custom syntax that allows for checking server memory **without SkUtilities**
# I recommend having a number precision of at least 6 to display the entire number, but it's whatever if you don't.

import:
	java.lang.Runtime

expression [server ](available|free) (memory|ram):
	get:
		set {_n} to Runtime.getRuntime().freeMemory()/1000000
		return {_n}

expression [server ]max[imum] (memory|ram):
	get:
		set {_n} to Runtime.getRuntime().maxMemory()/1000000
		return {_n}

expression [server ]used (memory|ram):
	get:
		set {_n} to max ram - free ram
		return {_n}

expression percent[age][ of [server ](memory|ram)] used[ up]:
	get:
		set {_p} to used memory/max memory
		return {_p}

expression percent[age][ of [server ](memory|ram)] free[d]:
	get:
		set {_p} to free memory/max memory
		return {_p}

function broadcastStats():
	broadcast "&5Max RAM: %max ram%"
	broadcast "&dFree RAM: %free ram%"
	broadcast "&bUsed RAM: %used ram%"
